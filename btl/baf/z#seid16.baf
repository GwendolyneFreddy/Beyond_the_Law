// Seida's script

// Jams the Script from going further midway
// -----------------------------------------
IF
	Global("Z#MeetingSeida","GLOBAL",6)
	Global("BregaIn","Z#0041",3)
	LOS("Z#Kni1",5)
	LOS("Z#Kni2",5)
THEN
	RESPONSE #100
		AddJournalEntry(@5023,QUEST)
		DestroyItem("MinHp1")
		DestroySelf()
END

IF
	!InMyArea(NearestEnemyOf([PC]))
	Global("Z#MeetingSeida","GLOBAL",5)
THEN
	RESPONSE #100
		SetGlobal("Z#MeetingSeida","GLOBAL",6)
		SetSequence(SEQ_SLEEP)
		SetGlobal("ShadowHunt","GLOBAL",0)
		SetGlobal("KKThief","GLOBAL",0)
		CreateCreature("observe",[995.70],0)
		CreateCreature("habrega",[995.70],2)
		CreateCreature("Z#MESSEN",[965.90],2)
		CreateCreature("Z#Kni1ok",[965.50],2)
		CreateCreature("Z#Kni2ok",[935.70],2)
		CreateCreature("Z#Kni3ok",[1025.50],2)
		CreateCreature("Z#Kni4ok",[995.70],2)
		ActionOverride("habrega",StartDialogNoSet(Player1))
END

// Defeated!
// ---------
IF
	HasItem("SCRL1M",Myself)
	Global("Z#Fallen","LOCALS",1)
	GlobalLT("Z#MeetingSeida","GLOBAL",5)
	GlobalTimerNotExpired("Z#FallenT","LOCALS")
THEN
	RESPONSE #100
		SetCutSceneLite(TRUE)
		SetGlobalTimer("Z#FallenT","LOCALS",15000)
		AddexperienceParty(22000)
		TakeItemReplace("CLCK06","CLCK01",Myself)
		ApplySpell(Myself,WIZARD_INVISIBILITY)
		SetGlobal("Z#MeetingSeida","GLOBAL",5)
		SetGlobal("Z#SeidaAssas","Z#0041",0)
		SetGlobal("Z#SeidaWizard","Z#0041",0)
		CreateCreatureObjectOffset("z#assas1",Myself,[-1.-1])
		ActionOverride("z#assas1",DestroyAllEquipment())
		ApplySpell("z#assas1",SLAVE_CHUNK)
		ActionOverride("STDeath",DestroyAllEquipment())
		ActionOverride("STDeath",DestroySelf())
		ChangeEnemyAlly(Myself,EVILBUTBLUE)
END

IF
	HasItem("SCRL1M",Myself)
	OR(2)
		!Global("Z#Fallen","LOCALS",1)
		!GlobalLT("Z#MeetingSeida","GLOBAL",7)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,@837)  // ~The Rune has no effect as Seida is conscious.~
		DestroyItem("SCRL1M")
END

// Arkanis shows up
// ----------------
IF
	!PartyHasItem("Z#Rune")
	Global("Z#Fallen","LOCALS",0)
	GlobalLT("Z#MeetingSeida","GLOBAL",5)
	GlobalGT("Z#MeetingSeida","GLOBAL",2)
	Global("Z#SpawnArkanis","Z#0041",0)
THEN
	RESPONSE #100
		SetGlobal("Z#SpawnArkanis","Z#0041",1)
		DisplayStringHead(Player1,@832)  // ~You get a bad feeling as you find yourself no longer having any Runes that Aran gave you...~
		CreateCreatureObjectOffset("STDeath",Player1,[-10.10])
END

// Remove Nearby Allies
// --------------------
IF
	Detect("Edwin")
	!InParty("Edwin")
	!Dead("Edwin")
	Global("Z#MeetingSeida","GLOBAL",5)
	Global("Z#EdwinLeave","Z#0041",0)
THEN
	RESPONSE #100
		SetGlobal("Z#EdwinLeave","Z#0041",1)
		ChangeEnemyAlly("Edwin",NEUTRAL)
		DisplayStringHead("Edwin",@836)  // ~Bah, looks like the idiot got himself killed. Now, I should be going...~
		ActionOverride("Edwin",ApplySpell(Myself,DRYAD_TELEPORT))
END

IF
	InMyArea(NearestEnemyOf([PC]))
//	!InParty(LastTrigger)
	Global("Z#MeetingSeida","GLOBAL",5)
THEN
	RESPONSE #30
		DisplayStringHead(NearestEnemyOf([PC]),@833)  // ~Let's get out of here...~
		ActionOverride(NearestEnemyOf([PC]),DestroySelf())
	RESPONSE #30
		DisplayStringHead(NearestEnemyOf([PC]),@834)  // ~No point fighting for a dead man.~
		ActionOverride(NearestEnemyOf([PC]),DestroySelf())
	RESPONSE #30
		DisplayStringHead(NearestEnemyOf([PC]),@835)  // ~Looks like he won't be needing anymore help.~
		ActionOverride(NearestEnemyOf([PC]),DestroySelf())
END

// Troll-like Recovery
// -------------------
IF
	DifficultyLT(HARD)
	HPLT(Myself,15)
	Global("Z#Fallen","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("Z#FallenT","LOCALS",15)
		DestroyItem("SCRL1M")
		SetGlobal("Z#Fallen","LOCALS",1)
		SetSequence(SEQ_DIE)
END

IF
	DifficultyGT(NORMAL)
	HPLT(Myself,2)
	Global("Z#Fallen","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobalTimer("Z#FallenT","LOCALS",10)
		DestroyItem("SCRL1M")
		SetGlobal("Z#Fallen","LOCALS",1)
		SetSequence(SEQ_DIE)
END

IF
	Global("Z#Fallen","LOCALS",1)
//	HP(Myself,1)
	GlobalLT("Z#MeetingSeida","GLOBAL",5)
	!GlobalTimerNotExpired("Z#FallenT","LOCALS")
THEN
	RESPONSE #100
		SetGlobal("Z#Fallen","LOCALS",0)
		DestroyItem("SCRL1M")
		SetSequence(SEQ_AWAKE)
		ApplySpell(Myself,CLERIC_HEAL)
END

// Timer Controllers Lead to Spawning

// Assassin Summoning
// ------------------
IF
	Global("Z#SeidaAssas","Z#0041",1)
	Global("Z#SeidaAssasTSet","Z#0041",1)
	NumCreatureLT("Z#Assas3",4)
	GlobalTimerExpired("Z#SeidaAssasT3","Z#0041")
THEN
	RESPONSE #100
		SetGlobal("Z#SpawnAssas","Z#0041",1)
		Continue()
END

IF
	Global("Z#SeidaAssas","Z#0041",1)
	Global("Z#SeidaAssasTSet","Z#0041",1)
	NumCreatureGT("Z#Assas3",3)
	NumCreatureLT("Z#Assas3",6)
	GlobalTimerExpired("Z#SeidaAssasT5","Z#0041")
THEN
	RESPONSE #100
		SetGlobal("Z#SpawnAssas","Z#0041",1)
		Continue()
END

IF
	Global("Z#SeidaAssas","Z#0041",1)
	Global("Z#SeidaAssasTSet","Z#0041",1)
	NumCreatureGT("Z#Assas3",5)
	NumCreatureLT("Z#Assas3",8)
	GlobalTimerExpired("Z#SeidaAssasT7","Z#0041")
THEN
	RESPONSE #100
		SetGlobal("Z#SpawnAssas","Z#0041",1)
		Continue()
END

// Cowlie Summoning
// ----------------
IF
	Global("Z#SeidaWizard","Z#0041",1)
	Global("Z#SeidaWizardTSet","Z#0041",1)
	!NumCreatureLT("Z#CW16",1)
	NumCreatureLT("Z#CW18",1)
	GlobalTimerExpired("Z#SeidaWizardT1","Z#0041")
THEN
	RESPONSE #100
		SetGlobal("Z#SpawnCW18","Z#0041",1)
		Continue()
END

IF
	Global("Z#SeidaWizard","Z#0041",1)
	Global("Z#SeidaWizardTSet","Z#0041",1)
	NumCreatureLT("Z#CW16",1)
	!NumCreatureLT("Z#CW18",1)
	GlobalTimerExpired("Z#SeidaWizardT1","Z#0041")
THEN
	RESPONSE #100
		SetGlobal("Z#SpawnCW16","Z#0041",1)
		Continue()
END

IF
	Global("Z#SeidaWizard","Z#0041",1)
	Global("Z#SeidaWizardTSet","Z#0041",1)
	NumCreatureLT("Z#CW16",1)
	NumCreatureLT("Z#CW18",1)
	GlobalTimerExpired("Z#SeidaWizardT0","Z#0041")
THEN
	RESPONSE #50
		SetGlobal("Z#SpawnCW16","Z#0041",1)
		Continue()
	RESPONSE #50
		SetGlobal("Z#SpawnCW18","Z#0041",1)
		Continue()
END

// ST Mage Summoning
// -----------------
IF
	Global("Z#SeidaWizard","Z#0041",2)
	Global("Z#SeidaWizardTSet","Z#0041",1)
	NumCreatureLT("Z#Mage2",1)
	GlobalTimerExpired("Z#SeidaWizardT0","Z#0041")
THEN
	RESPONSE #100
		SetGlobal("Z#SpawnSTMage","Z#0041",1)
		Continue()
END

IF
	Global("Z#SeidaWizard","Z#0041",2)
	Global("Z#SeidaWizardTSet","Z#0041",1)
	NumCreatureGT("Z#Mage2",0)
	NumCreatureLT("Z#Mage2",2)
	GlobalTimerExpired("Z#SeidaWizardT1","Z#0041")
THEN
	RESPONSE #100
		SetGlobal("Z#SpawnSTMage","Z#0041",1)
		Continue()
END

// Keep him unconscious
// --------------------
IF
	Global("Z#Fallen","LOCALS",1)
// 	HP(1)
	GlobalTimerNotExpired("Z#FallenT","LOCALS")
THEN
	RESPONSE #100
		SetSequence(SEQ_SLEEP)
END

// If injured drink potion
// -----------------------
IF
	HPPercentLT(Myself,60)
THEN
	RESPONSE #100
		GiveItemCreate("potn52",Myself,1,0,0)
		UseItem("potn52",Myself)
		Continue()
END

// If injured use Stoneskin
// ------------------------
IF
 	CheckStat(Myself,0,STONESKINS)
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("Z#StoneT","LOCALS")
	GlobalLT("Z#Stoneskin","LOCALS",3)
	HPPercentLT(Myself,40)
THEN
	RESPONSE #100
		SetGlobalTimer("Z#StoneT","LOCALS",20)
		IncrementGlobal("Z#Stoneskin","LOCALS",1)
		DisplayStringHead(Myself,@831)  // ~uses scroll~
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		Continue()
END

// If many close use Stoneskin
// ---------------------------
IF
 	CheckStat(Myself,0,STONESKINS)
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("Z#StoneT","LOCALS")
	GlobalLT("Z#Stoneskin","LOCALS",3)
	LOS(NearestEnemyOf(Myself),15)
	LOS(SecondNearestEnemyOf(Myself),15)
	LOS(ThirdNearestEnemyOf(Myself),15)
THEN
	RESPONSE #100
		SetGlobalTimer("Z#StoneT","LOCALS",20)
		IncrementGlobal("Z#Stoneskin","LOCALS",1)
		DisplayStringHead(Myself,@831)  // ~uses scroll~
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		Continue()
END

// Poison Weapon
// -------------
IF
 	Detect(NearestEnemyOf(Myself))
	!GlobalTimerNotExpired("Z#PoisonT","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("Z#PoisonT","LOCALS",30)
		ApplySpellRES("SPCL423",Myself)
		Continue()
END

// Lesser Whirlwind
// ----------------
IF
 	LOS(NearestEnemyOf(Myself),20)
//	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("Z#WhirlT","LOCALS")
THEN
	RESPONSE #35
		SetGlobalTimer("Z#WhirlT","LOCALS",20)
		SetGlobalTimer("Z#WWineffectT","LOCALS",6)
		ApplySpellRES("Z#SEIDWW",Myself)
		EquipMostDamagingMelee()
		AttackReevaluate(NearestEnemyOf(Myself),45)
	RESPONSE #65
		Continue()
END

// Go Invisible
// ------------
IF
	!StateCheck(Myself,STATE_INVISIBLE)
	!GlobalTimerNotExpired("Z#InvisT","LOCALS")
	!GlobalTimerNotExpired("Z#WWineffectT","LOCALS")
	!Allegiance(Myself,NEUTRAL)
THEN
	RESPONSE #100
		DisplayStringHead(Myself,46150)
		ReallyForceSpell(Myself,WIZARD_INVISIBILITY)
		SetGlobalTimer("Z#InvisT","LOCALS",4)
		Continue()
END

// If Whirlwind on attack
// ----------------------
IF
	LOS(NearestEnemyOf(Myself),20)
	GlobalTimerNotExpired("Z#WWineffectT","LOCALS")
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

// If invisible attack with melee
// ------------------------------
IF
	StateCheck(Myself,STATE_INVISIBLE)
	Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

// If near tried invis use melee 15
// --------------------------------
IF
	!StateCheck(Myself,STATE_INVISIBLE)
	GlobalTimerNotExpired("Z#InvisT","LOCALS")
	LOS(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

// If visible move away,use ranged
// --------------------------------
IF
	!StateCheck(Myself,STATE_INVISIBLE)
	GlobalTimerNotExpired("Z#InvisT","LOCALS")
	LOS(NearestEnemyOf(Myself),10)
	!LOS(NearestEnemyOf(Myself),5)
THEN
	RESPONSE #100
		RunAwayFrom(NearestEnemyOf(Myself),15)
END

// If Far enough,use Ranged
// -------------------------
IF
	!StateCheck(Myself,STATE_INVISIBLE)
	GlobalTimerNotExpired("Z#InvisT","LOCALS")
	!LOS(NearestEnemyOf(Myself),15)
THEN
	RESPONSE #100
		EquipRanged()
		AttackReevaluate(NearestEnemyOf(Myself),15)
END

// Edwin Shoots Seida

// Turn Hostile
// ------------
IF
	AttackedBy([GOODCUTOFF],DEFAULT)
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		Enemy()
END

// If Edwin is Dead
// ----------------
IF
	Dead("Edwin")
	Global("Z#Fallen","LOCALS",0)
	GlobalLT("Z#MeetingSeida","GLOBAL",5)
	Global("Z#CommentEdwinDie","LOCALS",0)
	Global("Z#SeidaWizard","Z#0041",0)
THEN
	RESPONSE #100
		SetGlobal("Z#CommentEdwinDie","LOCALS",1)
		ActionOverride("Z#Seid16",StartDialogNoSet(Player1))
END
